dnl
dnl Setup autoconf
dnl
AC_PREREQ([2.68])
AC_INIT([GAP], [4.dev], [support@gap-system.org], [gap], [http://www.gap-system.org/])

AC_CONFIG_SRCDIR([src/gap.c])
AC_CONFIG_HEADERS([src/config.h])
AC_CONFIG_MACRO_DIR([cnf/m4])
AC_CONFIG_AUX_DIR([cnf])

dnl
dnl Get canonical host information
dnl
AC_CANONICAL_HOST

# HACK
AC_DEFINE_UNQUOTED([SYS_ARCH], ["fake-gap-arch"], [define the name of the architucture])
AC_DEFINE_UNQUOTED([CONFIGNAME], ["fake-config"], [the CONFIGNAME value of the active configuration])
# GAPARCH ?

dnl
dnl Set the language
dnl
AC_PROG_CC
AC_LANG([C])


dnl
dnl Check whether to use _GNU_SOURCE etc. (needed for e.g. exp10)
dnl

AC_USE_SYSTEM_EXTENSIONS


dnl autoconf may set CXX to g++ even if there is no working C++ compiler.
dnl So verify that CXX really is able to compile C++ code.
AC_LANG_PUSH([C++])
AC_COMPILE_IFELSE([AC_LANG_PROGRAM(
  [[#include <iostream>
    #ifndef __cplusplus
    #error "broken C++"
    #endif]])],,
  [CXX=;])
AC_LANG_POP([C++])


dnl
dnl Misc
dnl
AC_PROG_INSTALL
AC_PROG_MKDIR_P


dnl
dnl Setup libtool (for interfacing with GAP kernel extension)
dnl
LT_PREREQ([2.4.2])
LT_INIT([disable-static dlopen win32-dll])

dnl
dnl Check for properties of the host system and compiler
dnl

dnl determine pointer sizes to distinguish 32 and 64 bit systems.
AC_CHECK_SIZEOF([void *])
AC_CHECK_SIZEOF([int])
AC_CHECK_SIZEOF([long])
AC_CHECK_SIZEOF([long long])

dnl endianess
AC_C_BIGENDIAN

dnl shifts
AC_MSG_CHECKING([whether right shifts are arithmetic])
AC_COMPUTE_INT(RIGHTSHIFTMINUSONE, [(-1L >> 1)+1])
if test x"$RIGHTSHIFTMINUSONE" = x0 ; then
    HAVE_ARITHRIGHTSHIFT=1
    AC_MSG_RESULT([yes])
else
    HAVE_ARITHRIGHTSHIFT=0
    AC_MSG_RESULT([no])
fi
AC_DEFINE_UNQUOTED([HAVE_ARITHRIGHTSHIFT],
    [$HAVE_ARITHRIGHTSHIFT],
    [define as 1 if >> for long int behaves like an arithmetic right shift for negative numbers])

dnl compiler builtins
AC_DEFUN([CHECK_COMPILER_BUILTIN],
[AC_MSG_CHECKING([for $1])
    AC_LINK_IFELSE(
        [AC_LANG_PROGRAM(
            [[]],
            [$1[($2)];
            ]
        )],
        [AS_VAR_SET([[have_]$1], [yes])],
        [AS_VAR_SET([[have_]$1], [no])]
        )
    AC_MSG_RESULT(AS_VAR_GET([[have_]$1]))
    AS_IF([test yes = AS_VAR_GET([[have_]$1])],
        [AC_DEFINE_UNQUOTED(AS_TR_CPP([HAVE_]$1), 1,
            [Define to 1 if the system has the `]$1[' built-in function])], []
        )])

CHECK_COMPILER_BUILTIN([__builtin_smul_overflow],[0,0,0]);
CHECK_COMPILER_BUILTIN([__builtin_smull_overflow],[0,0,0]);
CHECK_COMPILER_BUILTIN([__builtin_smulll_overflow],[0,0,0]);


dnl
dnl User settings
dnl

dnl HPC-GAP mode (off by default)
AC_ARG_ENABLE([hpcgap],
    [AS_HELP_STRING([--enable-hpcgap], [enable HPC-GAP])],
    [AC_DEFINE([HPCGAP], [1], [define if building HPC-GAP])],
    [enable_hpcgap=no]
    )
AC_MSG_CHECKING([whether to enable HPC-GAP])
AC_MSG_RESULT([$enable_hpcgap])

AC_SUBST([HPCGAP], [$enable_hpcgap])
if test "x$enable_hpcgap" = xyes ; then
    # HACK (see issue #9)
    AC_DEFINE([MAX_GC_THREADS], [4], [maximum number of GC threads])
fi

dnl Debug mode (off by default)
AC_ARG_ENABLE([debug],
    [AS_HELP_STRING([--enable-debug], [enable debug mode])],
    [AC_DEFINE([DEBUG], [1], [define if building in debug mode])],
    [enable_debug=no]
    )
AC_MSG_CHECKING([whether to enable debug mode])
AC_MSG_RESULT([$enable_debug])


dnl Compatibility mode for packages (on by default)
AC_ARG_ENABLE([compat-mode],
    [AS_HELP_STRING([--disable-compat-mode], [enable compatibility mode])],
    [],
    [enable_compat_mode=yes]
    )
AC_MSG_CHECKING([whether to enable compatibility mode for packages])
AC_MSG_RESULT([$enable_compat_mode])

AC_SUBST([COMPAT_MODE], [$enable_compat_mode])


dnl
dnl External dependencies
dnl

dnl Find GMP
AC_ARG_WITH([gmp],
  [AS_HELP_STRING([--with-gmp[=DIR]],  
                  [prefix of GMP installation. e.g. /usr/local or /usr])],
  [GMP_PREFIX="$with_gmp"],
  [AC_CHECK_LIB([gmp], [__gmpz_init],
    [
    GMP_USE_BUILTIN=no
    if test "x$GMP_PREFIX" != x ; then
      GMP_CPPFLAGS=" -I${GMP_PREFIX}/include"
      GMP_LDFLAGS=" -L${GMP_PREFIX}/lib -lgmp"
    else
      GMP_CPPFLAGS=""
      GMP_LDFLAGS="-lgmp"
    fi
    ],
    [
    AC_CONFIG_SUBDIRS([extern/gmp])
    GMP_USE_BUILTIN=yes
    GMP_PREFIX='${builddir}/extern/gmp'
    GMP_CPPFLAGS=" -I${GMP_PREFIX}"
    GMP_LDFLAGS=" -L${GMP_PREFIX} -lgmp"
    AC_MSG_NOTICE([No usable GMP found, switching to included GMP])
#     AC_MSG_ERROR([GNU Multiple Precision Arithmetic Library not found.
#     Maybe you want to call configure with the --with-gmp=<path> option?
#     This tells configure where to find the GMP library and headers.
#     e.g. --with-gmp=/usr/local or --with-gmp=/usr])
    ])]
)
AC_SUBST([GMP_USE_BUILTIN])
AC_SUBST([GMP_PREFIX])
AC_SUBST([GMP_CPPFLAGS])
AC_SUBST([GMP_LDFLAGS])
AC_DEFINE([USE_GMP], [1], [for backwards compatibility])


dnl Find GNU readline
dnl We check for the symbol rl_ding to distinguish GNU readline
dnl from other readline implementation.
AC_ARG_WITH([readline],
  [AS_HELP_STRING([--with-readline],
    [support fancy command line editing @<:@default=check@:>@])],
  [],
  [with_readline=check])

READLINE_LDFLAGS=
use_readline=no
AS_IF([test "x$with_readline" != xno],
  [AC_CHECK_LIB([readline], [rl_ding],
    [
      AC_CHECK_HEADER(readline/readline.h,
        [AC_CHECK_HEADER(readline/history.h, use_readline=yes)]
      )
    ],
    [], -lncurses)
  ]
)
AS_IF([test "x$use_readline" = xyes],
    [READLINE_LDFLAGS="-lreadline -lncurses"
     AC_DEFINE([HAVE_LIBREADLINE], [1],
               [Define if you have libreadline])
    ],
    [if test "x$with_readline" != xcheck; then
       AC_MSG_FAILURE(
         [--with-readline was given, but test for readline failed])
     fi
    ]
)
AC_SUBST([READLINE_LDFLAGS])


dnl Find ward (if HPC-GAP mode is enabled)
AC_ARG_WITH([ward],
  [AS_HELP_STRING([--with-ward],
    [prefix to the ward tool (used for building HPC-GAP) @<:@default=check@:>@])],
  [],
  [with_ward=check])

dnl If HPC-GAP is enabled, check for a usable ward, either in the prefix
dnl specified by the user, or else in $srcdir/hpcgap/ward
AS_IF([test "x$enable_hpcgap" = xyes],
  [
  # If the user specified a prefix, check only there.
  # Otherwise, check in various relative paths, and in $PATH
  AS_IF([test "x$with_ward" != xcheck],
        [WARD_PATH="$with_ward/bin"],
        [WARD_PATH="$srcdir/hpcgap/ward/bin:ward/bin:../ward/bin:$PATH"]
        )
  AC_PATH_PROG([WARD], [ward], [], [$WARD_PATH])
  AS_IF([test "x$WARD" = x], AC_MSG_ERROR([could not locate ward]))

  AC_PATH_PROG([ADDGUARDS], [addguards], [], [$WARD_PATH])
  AS_IF([test "x$ADDGUARDS" = x], AC_MSG_ERROR([could not locate addguards]))

  AC_PATH_PROG([ADDGUARDS2], [addguards2], [], [$WARD_PATH])
  AS_IF([test "x$ADDGUARDS2" = x], AC_MSG_ERROR([could not locate addguards2]))


# TODO: there are further hpcgap specific external dependencies:
#  gc - the Boehm garbage collector, see http://www.hboehm.info/gc/
#  libatomic_ops - part of gc, but see also https://github.com/ivmai/libatomic_ops
#  ...

# HACK
AC_DEFINE([BOEHM_GC], [1], [Use Boehm garbage collector])

# TODO: detect BOEHM GC and set flags suitably
#  -> may want to build the one shipped with HPC-GAP,
#  as that has special improvements for HPC-GAP
if test "x$enable_hpcgap" = xyes ; then
  BOEHM_LDFLAGS="-lgc"
fi
AC_SUBST([BOEHM_CPPFLAGS])
AC_SUBST([BOEHM_LDFLAGS])

# TODO: detected libatomic and set flags suitably
#  -> may want to build the one shipped with HPC-GAP,
#  as that has special improvements for HPC-GAP
AC_SUBST([LIBATOMIC_OPS_CPPFLAGS])
AC_SUBST([LIBATOMIC_OPS_LDFLAGS])

# FIXME: what about these defines which HPC-GAP seems to "know"?
# - DISABLE_GC
# - NUM_CPUS
# - VERBOSE_GUARDS

  ]
)



dnl
dnl Detect host specific setting
dnl

GP_C_LONG_ALIGN


case "$host_cpu" in 
  sparc* )
    AC_DEFINE([SPARC], [1], [define as 1 on SPARC architecture to flush register windows])
    SPARC=yes
    ;;
  ia64* ) 
    AC_DEFINE([ITANIUM], [1], [define as 1 on Itanium architecture to flush and mark register stack])
    ITANIUM=yes
    ;;
esac

case "$host_os" in
  *cygwin*)
    AC_DEFINE([SYS_IS_CYGWIN32], [1], [define if this is the Cygwin32 port])
    CYGWIN=yes
    ;;
  *darwin*)
    AC_DEFINE([SYS_IS_DARWIN], [1], [define if this is the Darwin port])
    DARWIN=yes
    ;;
esac

dnl Note: AC_DEFINE defines conditionals to be put in config.h, while
dnl AM_CONDITIONAL defines conditionals for use in in Makefile.am
AC_SUBST([ITANIUM], [$ITANIUM])
AC_SUBST([SYS_IS_CYGWIN32], [$CYGWIN])
SYS_IS_DARWIN=$DARWIN
AC_SUBST([SYS_IS_DARWIN])


dnl
dnl check for the existence of various header files
dnl

# FIXME: needed?
#AC_HEADER_STDC


dnl check for stdint.h and the types int8_t, uint8_t etc. it defines
AC_TYPE_INT8_T
AC_TYPE_INT16_T
AC_TYPE_INT32_T
AC_TYPE_INT64_T
AC_TYPE_UINT8_T
AC_TYPE_UINT16_T
AC_TYPE_UINT32_T
AC_TYPE_UINT64_T

dnl
dnl check for functionality related to child process handling,
dnl including pseudo TTY support, signals, etc.
dnl
dnl TODO: reduce these checks to the minimum! but make sure
dnl to also adjust the C code simultaneously, and e.g. remove
dnl checks for things like HAVE_SYS_WAIT_H if appropriate.
dnl

AC_CHECK_HEADERS([fcntl.h termios.h termio.h sgtty.h])
AC_CHECK_HEADERS([sys/ioctl.h sys/resource.h sys/stat.h sys/wait.h])

# FIXME: get rid of the following (requires code changes!!!)
AC_CHECK_HEADERS( assert.h errno.h math.h stdio.h stdlib.h string.h )


# Check for POSIX 98 pty APIs
AC_CHECK_FUNCS([ptsname grantpt unlockpt posix_openpt])

# Check for glibc specific pty APIs
AC_CHECK_FUNCS([getpt ptsname_r])

# Check for some legacy APIs
AC_CHECK_FUNCS([getpseudotty _getpty])

# openpty() is available on various BSD variants, but also in glibc.
# On BSD systems, one usually needs to add -lutil to LIBS in order
# to use it.
AC_CHECK_HEADERS([util.h pty.h libutil.h])
AC_SEARCH_LIBS([openpty], [util],
    AC_DEFINE([HAVE_OPENPTY], [1], [define as 1 if you have `openpty']))


dnl check for input/output functions
AC_CHECK_HEADERS([signal.h])
AC_CHECK_FUNCS([ttyname select])

dnl various functions to deal with child processes
AC_HEADER_SYS_WAIT
AC_TYPE_PID_T
AC_FUNC_FORK
AC_CHECK_FUNCS([popen])

dnl signal handling
AC_CHECK_TYPE([sig_atomic_t], [],
    [AC_DEFINE([HAVE_SIG_ATOMIC_T],[],[Check for sig_atomic_t])],
    [#include <signal.h>]
)
AC_CHECK_FUNCS([signal sigaction setpgid])


dnl
dnl check for dynamic loading of modules
dnl TODO: replace with libtool code?!
dnl

AC_CHECK_FUNCS([rld_load])

AC_SEARCH_LIBS([dlopen], [dl],
    [AC_DEFINE([HAVE_DLOPEN], [1], [define as 1 if you have `dlopen' and `dlsym'])]
)


dnl error handling
AC_CHECK_HEADERS([errno.h])

dnl check for timing functions
AC_CHECK_FUNCS([times getrusage gettimeofday])
AC_CHECK_FUNCS([setitimer getitimer])
AC_CHECK_LIB([rt], [timer_create])
AC_CHECK_FUNCS([timer_settime timer_gettime timer_create])
AC_CHECK_FUNCS([sys_timer_settime sys_timer_gettime sys_timer_create])

dnl check for functions dealing with virtual memory
AC_CHECK_FUNCS([vm_allocate sbrk madvise sysconf])

dnl check for functions dealing with strings and integers
AC_CHECK_FUNCS([atol strlcpy strlcmp strlcat])

dnl check for file access functions
AC_HEADER_STAT
AC_CHECK_FUNCS([access stat fstat lstat unlink mkdir rmdir mkdtemp mkstemp link rename chmod dup dup2 realpath])

dnl check for large-file support (for accessing files whose sizes or inodes require 64bits)
AC_SYS_LARGEFILE

dnl check for socket access functions
AC_CHECK_FUNCS([socket accept connect bind send])

dnl check for directory access functions
AC_HEADER_DIRENT
AC_CHECK_FUNCS([opendir closedir readdir])

dnl check whether libm / -lm is available and necessary
AC_SEARCH_LIBS([cos], [m], [], [
  AC_MSG_ERROR([unable to find the cos() function])
])

dnl check for math functions
AC_CHECK_FUNCS([log2 log10 log1p exp2 expm1 exp10 trunc])

dnl sigsetjmp is a macro on some platforms, cannot use AC_CHECK_FUNCS
AC_MSG_CHECKING([for sigsetjmp])
AC_TRY_LINK([#include <setjmp.h>], [sigjmp_buf t; sigsetjmp(t, 0)],
  [AC_MSG_RESULT([yes])
   AC_DEFINE([HAVE_SIGSETJMP], [1], [Define to 1 if you have the `sigsetjmp' function.])],
  [AC_MSG_RESULT([no])]
)

dnl _setjmp is a macro on some platforms, cannot use AC_CHECK_FUNCS
AC_MSG_CHECKING([for _setjmp])
AC_TRY_LINK([#include <setjmp.h>], [sigjmp_buf t; _setjmp(t, 0)],
  [AC_MSG_RESULT([yes])
   AC_DEFINE([HAVE__SETJMP], [1], [Define to 1 if you have the `_setjmp' function.])],
  [AC_MSG_RESULT([no])]
)

# for rusage and timers
AC_CHECK_HEADERS( time.h sys/time.h sys/times.h )
AC_CHECK_FUNCS( times getrusage gettimeofday clock_gettime clock_getres )
AC_CHECK_FUNCS( setitimer getitimer)
AC_CHECK_LIB( [rt], [timer_create])
AC_CHECK_FUNCS( timer_settime timer_gettime timer_create )
AC_CHECK_FUNCS( sys_timer_settime sys_timer_gettime sys_timer_create )

dnl pthreads
if test "x$enable_hpcgap" = xyes ; then
  AX_PTHREAD
fi


dnl
dnl Output everything
dnl
AC_CONFIG_FILES([GNUmakefile])
AC_OUTPUT
